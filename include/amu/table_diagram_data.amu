/+

  create parameter diagrams and data table.

  requires:
    mfs-scope: ${scope_id}_{diagram,table}

    defines:  ${data_name}
              ${image_views}
              ${image_size}

    optional: ${diagram_notes}
              ${data_notes}

              ${scope_id}               (default = db)

              ${image_columns}          (default = 3)
              ${html_image_w}           (default = 256)
              ${latex_image_w}          (default = 1.5in)

 +/

  \amu_if scope_id (-d ${scope_id}) {${scope_id}} else {data} endif

  \amu_if image_columns (-d ${image_columns}) {${image_columns}} else {3} endif
  \amu_if html_image_w (-d ${html_image_w}) {${html_image_w}} else {256} endif
  \amu_if latex_image_w (-d ${latex_image_w}) {${latex_image_w}} else {1.5in} endif

  \amu_scope scope_root (++root)

  /+
    diagram table
  +/

  \amu_foreach png_files
  (
    words="${image_views}"
    text="${OUTPUT_PREFIX}/png/${scope_root}_${scope_id}_diagram_${image_size}_\${x}.png"
    separator=" "
  )

  \amu_foreach eps_files
  (
    words="${image_views}"
    text="${OUTPUT_PREFIX}/png2eps/${scope_root}_${scope_id}_diagram_${image_size}_\${x}.eps"
    separator=" "
  )

  \amu_foreach cell_captions
  (
    words="${image_views}"
    text="\${x}"
    separator="^"
  )

  \amu_foreach cell_titles
  (
    words="${image_views}"
    text="expand \${x}"
    separator="^"
  )

  \amu_if (-d ${diagram_notes}) {${diagram_notes}} endif

  \htmlonly
    \amu_image_table
      (
        type=html
        columns=${image_columns}
        image_width="${html_image_w}"
        table_caption="${data_name} parameters"
        cell_files="${png_files}"
        cell_captions="${cell_captions}"
        cell_titles="${cell_titles}"
        cell_urls="${png_files}"
      )
  \endhtmlonly
  \latexonly
    \amu_image_table
      (
        type=latex
        columns=${image_columns}
        image_width="${latex_image_w}"
        table_caption="${data_name} parameters"
        cell_files="${eps_files}"
        cell_captions="${cell_captions}"
      )
  \endlatexonly

  /+
    data table

    assumes:  (1) first line is table headings.
              (2) remaining lines are table data.
  +/

  \amu_file th (file="${scope_root}_${scope_id}_table.log" first=1 last=1 ++rmecho ++rmnl ++read)
  \amu_file td (file="${scope_root}_${scope_id}_table.log" first=2        ++rmecho ++rmnl ++read)
  \amu_word tc (tokenizer="^" words="${th}" ++count)

  \amu_if (-d ${data_notes}) {${data_notes}} endif

  <b>\amu_eval(${data_name}) data table</b>

  \amu_table (columns=${tc} column_headings=${th} cell_texts=${td})

/+ end of file  +/
